// Initial import of postgres rules

sql_explain_stmt:
    EXPLAIN sql_explainable_stmt {
        PGExplainStmt *n = makeNode(PGExplainStmt);
        n->query = $2;
        n->options = NIL;
        $$ = (PGNode *) n;
    }
  | EXPLAIN sql_analyze_keyword sql_opt_verbose sql_explainable_stmt {
        PGExplainStmt *n = makeNode(PGExplainStmt);
        n->query = $4;
        n->options = list_make1(makeDefElem("analyze", NULL, @2));
        if ($3)
            n->options = lappend(n->options,
                                makeDefElem("verbose", NULL, @3));
        $$ = (PGNode *) n;
    }
  | EXPLAIN VERBOSE sql_explainable_stmt {
        PGExplainStmt *n = makeNode(PGExplainStmt);
        n->query = $3;
        n->options = list_make1(makeDefElem("verbose", NULL, @2));
        $$ = (PGNode *) n;
	}
  | EXPLAIN '(' sql_explain_option_list ')' sql_explainable_stmt {
        PGExplainStmt *n = makeNode(PGExplainStmt);
        n->query = $5;
        n->options = $3;
        $$ = (PGNode *) n;
	}
  ;

sql_opt_verbose:
    VERBOSE     {}
  | %empty      { $$ = Null(); }
  ;

sql_explain_option_arg:
    sql_opt_boolean_or_string	{ $$ = (PGNode *) makeString($1); }
  | sql_numeric			        { $$ = (PGNode *) $1; }
  | %empty			            { $$ = NULL; }
  ;

sql_explainable_stmt:
    sql_select_stmt
  | sql_create_stmt
  | sql_create_as_stmt
  | sql_view_stmt
  ;

sql_non_reserved_word:	
    IDENT                   { $$ = $1; }
  | sql_unreserved_keyword  { $$ = pstrdup($1); }
  | other_keyword           { $$ = pstrdup($1); }
  ;

sql_non_reserved_word_or_sconst:
    sql_non_reserved_word   { $$ = $1; }
  | SCONST                  { $$ = $1; }
  ;

sql_explain_option_list:
    sql_explain_option_elem                             { $$ = list_make1($1); }
  | sql_explain_option_list ',' sql_explain_option_elem { $$ = lappend($1, $3); }
  ;

sql_analyze_keyword:
    ANALYZE                 {}
  | ANALYSE /* British */   {}
  ;

sql_opt_boolean_or_string:
    TRUE_P      { $$ = (char*) "true"; }
  | FALSE_P     { $$ = (char*) "false"; }
  | ON          { $$ = (char*) "on"; }
  | NonReservedWord_or_Sconst	{ $$ = $1; }
  ;

sql_explain_option_elem:
    sql_explain_option_name sql_explain_option_arg {
	    $$ = makeDefElem($1, $2, @1);
    }
	;

sql_explain_option_name:
    NonReservedWord         { $$ = $1; }
  | sql_analyze_keyword     { $$ = (char*) "analyze"; }
  ;
